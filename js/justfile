#!/usr/bin/env just --justfile

# Using Just: https://github.com/casey/just?tab=readme-ov-file#installation

# List all of the available commands.
default:
  just --list

# Run the web server
web url='http://localhost:4443/anon':
	pnpm i
	VITE_RELAY_URL="{{url}}" pnpm -r run dev

# Run the CI checks
check flags="--frozen-lockfile":
	pnpm install {{flags}}

	# Make sure Typescript compiles
	pnpm -r run check

	# Run the JS tests via node.
	pnpm -r test

	# Format/lint the JS packages
	pnpm exec biome check

	# TODO: Check for unused imports (fix the false positives)
	# pnpm exec knip --no-exit-code

# Automatically fix some issues.
fix flags="--frozen-lockfile":
	# Fix the JS packages
	pnpm install {{flags}}

	# Format and lint
	pnpm exec biome check --fix

# Upgrade any tooling
upgrade:
	# Update the NPM dependencies
	pnpm self-update
	pnpm -r update
	pnpm -r outdated

# Build the packages
build flags="--frozen-lockfile":
	pnpm install {{flags}}
	pnpm -r run build
